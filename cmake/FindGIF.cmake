find_path(GIF_INCLUDE_DIR gif_lib.h PATH_SUFFIXES include)

if(NOT GIF_LIBRARIES OR NOT GIF_LIBRARY_RELEASE OR NOT GIF_LIBRARY_DEBUG)
  get_filename_component(GIF_ROOT_DIR ${GIF_INCLUDE_DIR} DIRECTORY)
  find_library(GIF_LIBRARY_RELEASE NAMES gif NAMES_PER_DIR
    NO_DEFAULT_PATH PATHS ${GIF_ROOT_DIR}/lib PATH_SUFFIXES lib)
  find_library(GIF_LIBRARY_DEBUG NAMES gif gifd NAMES_PER_DIR
    NO_DEFAULT_PATH PATHS ${GIF_ROOT_DIR}/debug/lib PATH_SUFFIXES lib)
  include(SelectLibraryConfigurations)
  select_library_configurations(GIF)
else()
  file(TO_CMAKE_PATH "${GIF_LIBRARY_RELEASE}" GIF_LIBRARY_RELEASE)
  file(TO_CMAKE_PATH "${GIF_LIBRARY_DEBUG}" GIF_LIBRARY_DEBUG)
  file(TO_CMAKE_PATH "${GIF_LIBRARIES}" GIF_LIBRARIES)
endif()

if(GIF_INCLUDE_DIR AND EXISTS "${GIF_INCLUDE_DIR}/gif_lib.h")
  file(STRINGS "${GIF_INCLUDE_DIR}/gif_lib.h" GIF_VERSION_HEADER
    REGEX "^[ \t]*#define[ \t]+GIFLIB_(MAJOR|MINOR|RELEASE)")

  string(REGEX REPLACE ".*GIFLIB_MAJOR ([0-9]+).*" "\\1"
    GIF_VERSION_MAJOR "${GIF_VERSION_HEADER}")
  string(REGEX REPLACE ".*GIFLIB_MINOR ([0-9]+).*" "\\1"
    GIF_VERSION_MINOR "${GIF_VERSION_HEADER}")
  string(REGEX REPLACE ".*GIFLIB_RELEASE ([0-9]+).*" "\\1"
    GIF_VERSION_PATCH "${GIF_VERSION_HEADER}")

  set(GIF_VERSION "${GIF_VERSION_MAJOR}.${GIF_VERSION_MINOR}.${GIF_VERSION_PATCH}")
  set(GIF_VERSION_STRING "${GIF_VERSION}")
  unset(GIF_VERSION_HEADER)
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(GIF
  REQUIRED_VARS GIF_INCLUDE_DIR GIF_LIBRARIES GIF_LIBRARY_RELEASE GIF_LIBRARY_DEBUG
  VERSION_VAR GIF_VERSION_STRING)

if(GIF_FOUND)
  set(GIF_INCLUDE_DIRS ${GIF_INCLUDE_DIR})
  if(NOT TARGET GIF::GIF)
    add_library(GIF::GIF UNKNOWN IMPORTED)
    set_target_properties(GIF::GIF PROPERTIES
      INTERFACE_INCLUDE_DIRECTORIES "${GIF_INCLUDE_DIR}"
      IMPORTED_LINK_INTERFACE_LANGUAGES "C"
      IMPORTED_CONFIGURATIONS "DEBUG;RELEASE"
      IMPORTED_LOCATION_RELEASE "${GIF_LIBRARY_RELEASE}"
      IMPORTED_LOCATION_DEBUG "${GIF_LIBRARY_DEBUG}"
      MAP_IMPORTED_CONFIG_RELWITHDEBINFO Release
      MAP_IMPORTED_CONFIG_MINSIZEREL Release)
    if(DEFINED CMAKE_BUILD_TYPE)
      if(CMAKE_BUILD_TYPE MATCHES "Debug")
        set(GIF_LIBRARY "${GIF_LIBRARY_DEBUG}")
      else()
        set(GIF_LIBRARY "${GIF_LIBRARY_RELEASE}")
      endif()
      mark_as_advanced(GIF_LIBRARY)
      set_property(TARGET GIF::GIF APPEND PROPERTY
        IMPORTED_LOCATION "${GIF_LIBRARY}")
    else()
      set(GIF_LIBRARY "${GIF_LIBRARIES}")
    endif()
  endif()
endif()

mark_as_advanced(GIF_INCLUDE_DIR)
