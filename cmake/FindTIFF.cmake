if(TIFF_FOUND)
  return()
endif()

if(NOT TIFF_INCLUDE_DIR)
  find_path(TIFF_INCLUDE_DIR tiff.h PATH_SUFFIXES include)

  set(TIFF_INCLUDE_DIRS "${TIFF_INCLUDE_DIR}")

  mark_as_advanced(
    TIFF_INCLUDE_DIR
    TIFF_INCLUDE_DIRS)
endif()  

if(NOT TIFF_LIBRARIES)
  include(SelectLibraryConfigurations)
  get_filename_component(TIFF_ROOT_DIR ${TIFF_INCLUDE_DIR} DIRECTORY)

  find_library(TIFF_LIBRARY_RELEASE NAMES tiff NAMES_PER_DIR
    NO_DEFAULT_PATH PATHS ${TIFF_ROOT_DIR}/lib PATH_SUFFIXES lib)
  find_library(TIFF_LIBRARY_DEBUG NAMES tiff tiffd NAMES_PER_DIR
    NO_DEFAULT_PATH PATHS ${TIFF_ROOT_DIR}/debug/lib PATH_SUFFIXES lib)

  select_library_configurations(TIFF)

  mark_as_advanced(
    TIFF_LIBRARY_RELEASE
    TIFF_LIBRARY_DEBUG
    TIFF_LIBRARIES)
endif()

if(NOT TIFF_VERSION_STRING AND EXISTS "${TIFF_INCLUDE_DIR}/tiffvers.h")
  file(STRINGS "${TIFF_INCLUDE_DIR}/tiffvers.h" TIFF_VERSION_STRING
    REGEX "^#define[\t ]+TIFFLIB_VERSION_STR[\t ]+\"LIBTIFF, Version .*")

  string(REGEX REPLACE "^#define[\t ]+TIFFLIB_VERSION_STR[\t ]+\"LIBTIFF, Version +([^ \\n]*).*"
    "\\1" TIFF_VERSION "${TIFF_VERSION_STRING}")

  set(TIFF_VERSION_STRING "${TIFF_VERSION}")

  mark_as_advanced(
    TIFF_VERSION
    TIFF_VERSION_STRING)
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(TIFF
  REQUIRED_VARS
    TIFF_INCLUDE_DIR
    TIFF_LIBRARIES
    TIFF_LIBRARY_RELEASE
    TIFF_LIBRARY_DEBUG
  VERSION_VAR
    TIFF_VERSION_STRING)

if(TIFF_FOUND)
  if(NOT TARGET TIFF::TIFF)
    add_library(TIFF::TIFF UNKNOWN IMPORTED)
    set_target_properties(TIFF::TIFF PROPERTIES
      INTERFACE_INCLUDE_DIRECTORIES "${TIFF_INCLUDE_DIR}"
      IMPORTED_LINK_INTERFACE_LANGUAGES "C"
      IMPORTED_CONFIGURATIONS "DEBUG;RELEASE"
      IMPORTED_LOCATION_RELEASE "${TIFF_LIBRARY_RELEASE}"
      IMPORTED_LOCATION_DEBUG "${TIFF_LIBRARY_DEBUG}"
      MAP_IMPORTED_CONFIG_RELWITHDEBINFO Release
      MAP_IMPORTED_CONFIG_MINSIZEREL Release)
  endif()

  if(DEFINED CMAKE_BUILD_TYPE)
    if(CMAKE_BUILD_TYPE MATCHES "Debug")
      set(TIFF_LIBRARY "${TIFF_LIBRARY_DEBUG}")
    else()
      set(TIFF_LIBRARY "${TIFF_LIBRARY_RELEASE}")
    endif()
    set_property(TARGET TIFF::TIFF APPEND PROPERTY
      IMPORTED_LOCATION "${TIFF_LIBRARY}")
  else()
    set(TIFF_LIBRARY "${TIFF_LIBRARIES}")
  endif()
  mark_as_advanced(TIFF_LIBRARY)
endif()
